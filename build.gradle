plugins {
  id 'org.springframework.boot' version '2.1.9.RELEASE'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'java'
  id 'eclipse'
}

// project configuration
project.sourceCompatibility = '1.8'
project.archivesBaseName = 'toilet'
project.version = '0.0.0'

sourceSets {
  main {
    resources {
      srcDir 'src/main/web'
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url 'http://oss.sonatype.org/content/repositories/snapshots'
  }
}

configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-jetty'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'io.pebbletemplates:pebble-spring-boot-starter:3.1.0'
  implementation 'com.hectorlopezfernandez.pebble:spring-security-extension:2.0.0-SNAPSHOT'
  implementation 'javax.inject:javax.inject:1'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test:5.1.6.RELEASE'
  testImplementation 'de.bwaldvogel:mongo-java-server:1.21.0'
}

// local run marker task
task start(dependsOn: bootRun) {
  outputs.upToDateWhen { false }
}


// model class export for toilet-initializer

task modelClassesJar(type: Jar, dependsOn: classes) {
   classifier = 'models'
   from sourceSets.main.output.classesDirs
   includeEmptyDirs false
   include '**/Authority*.class', '**/User*.class', '**/Language*.class', '**/Preferences*.class', '**/Page*.class', '**/Post*.class'
   exclude '**/*Service*.class'
}

artifacts {
   archives modelClassesJar
}
